! ===============================================
! GLE Example
! ==============================================
! Let us set the size of the page for our image.
! By default, all measurements in GLE are in
! centimetres for lengths, and degrees for angles.
! ----------------------
pageX = 21
pageY = 21
size pageX pageY

! ----------------------
! Set the page background colour to white

set color white
amove 0 0
box pageX pageY fill white

! ----------------------
! Now set the defauly colour to black (for lines and fonts etc)

set color black

! ----------------------
! first move to (1cm,1cm) from the bottom-left
! corner, and then create a yellow box of size
! 3cm x 3.5cm :

amove 1 0.5
box 3 3.5 fill yellow

! ----------------------
! see how it has a black border?  Let\'s add a box
! next to it with the "noborder option included:

amove 5 0.5
box 3 3.5 fill yellow noborder

! ----------------------
! Now the border has gone!! :)  Now let\'s do a
! third box with a yellow border:

set color yellow
amove 9 0.5
box 3 3.5 fill yellow

! ----------------------
! And finally, we can set the border color to be
! whatever we like, by setting the GLE default
! colour before we draw the box:

set color red
amove 13 0.5
box 3 3.5 fill yellow

! ----------------------
! Let\'s look at some of the other things that GLE
! can do...

! We can create circles:

set color magenta
amove 18.5 2.5
circle 1.75 fill cyan

! ----------------------
! We can also change GLE\'s line thickness.
! The line thickness is also measured in cm
! and affects the tickness of borders, and any
! other lines drawn (such as arrows!):

set lwidth 0.1
amove 2.5 6.5
set color red
circle 1.75 fill blue

set lwidth 0.3
amove 6.5 6.5
set color red
circle 1.75 fill blue

! ----------------------
! This is starting to look like a traffic lollipop
! sign, so let\'s make one!

set lwidth 0.5
amove 4 17
set color red
circle 3 fill yellow

! ----------------------
! add some text to the lollipop sign:
! set the font colour as black:
set color black
! make the font colour large:
set hei 1.6
! set the font type to something non-fancy:
set font ssb
! Now write the text:
amove 1.9 17.6
text STOP
! now draw a line with rounded ends:
amove 1.8 17
set lwidth 0.8
set cap round
rline 4.4 0

! ----------------------
! GLE also allows you to add images you\'ve already
! created, in JPG, PNG or EPS format. Let\'s add
! a PNG image of some pedestrians...
! I will set the width of the image to 2.3cm and
! let GLE decide what the best height is, by
! specifying "" for the height:
amove 2.8 14.6
bitmap "pedestrians.png" 2.3 1.9

! ----------------------
! Let\'s add a picture of a tree to the mid-right
! of the page, and put a green frame around it:
set lwidth 0.2
amove 12.5 5.5
bitmap "tree.jpg" 7.5 7.5
set color green
box 7.5 7.5 fill clear

! ----------------------
! Now add a label to the image in blue, using a
! fancy font:
rmove -0.5 -0.8
set lwidth 0
set hei 0.6
set font plss
set color blue
text Image1 - a tree during Autumn

! ----------------------
! I can also add some exciting things in LATEX!
! Don\'t worry about this part if you don\'t know
! what LATEX is! :)

! First you can add your Preamble like so:

begin texpreamble
\usepackage{amsmath}
\usepackage{amssymb}
\usepackage[dvips]{graphicx}
\usepackage{epsfig}
\usepackage{txfonts}
\usepackage{color}
end texpreamble

! And you can add LATEX like this:
amove 1 12.5
tex "Here is some \TeX"

! or like this:

amove 1 11.5
text \tex{Here are some maths symbols: $\alpha, \beta, \gamma $\ldots}

! or like this:

amove 1 10
begin tex
\begin{tabular}{|c|c|c|}
\hline\hline
 Set 1 & 0.2 & 40\\
\hline
 Set 2 & 0.1 & 30\\
\hline
\hline
\end{tabular}
end tex

! ----------------------
! Finally, you can also add graphs and things:
! Graphs are the most complicated things you
! might want to do in GLE:

set color black
set hei 0.4
amove 10 14.5
begin graph
size 10 5
fullsize
   title "f(x) = sin(x)"
   xaxis min -2*pi max 2*pi ftick -2*pi dticks pi/2 format "pi"
   yaxis min -1.1 max 1.1 dticks 0.25
   let d1 = sin(x)
   d1 line color red
end graph
set color blue
amove xg(xgmin) 0.5*(yg(ygmin)+yg(ygmax))
rline xg(xgmax)-xg(xgmin) 0

! There\'s a whole lot more you can do with GLE,
! so be sure to check out the GLE webpage!
! There you\'ll find some nice examples to follow.
! ----------------------

